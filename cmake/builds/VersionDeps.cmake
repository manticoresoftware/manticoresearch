# Deps versions parsing
file(STRINGS "${MANTICORE_SOURCE_DIR}/deps.txt" lines)
foreach(line ${lines})
	if(line STREQUAL "---")
		break()
	endif()

	message("Parsing deps.txt line: ${line}")
	string(REGEX MATCH "^([^ ]+) ([^+ ]+)[+ ]*([^ -]*)[ -]*([^ -]*)[ -]*([^ ]*)" _ "${line}")
	string(TOUPPER ${CMAKE_MATCH_1} dep)
	set(vernum ${CMAKE_MATCH_2})
	set(verdate ${CMAKE_MATCH_3})
	if (CMAKE_MATCH_5)
	  set("${dep}_VERHASH" "${CMAKE_MATCH_4}-${CMAKE_MATCH_5}")
	else ()
	  set("${dep}_VERHASH" "${CMAKE_MATCH_4}")
	endif ()
	set("${dep}_VERNUM" ${vernum})
	set("${dep}_VERDATE" ${verdate})

	# Increment first number in semver and assign it to max (incompatible)
	string(REGEX REPLACE "^([0-9]+)\\..*$" "\\1" major_version "${CMAKE_MATCH_2}")
	math(EXPR major_version "${major_version} + 1")
	set(vernum_max "${major_version}.0.0")
	set("${dep}_VERNUM_MAX" ${vernum_max})

	# VERHASH now includes -dev suffix if present (for dev builds) or just the hash (for release builds)
	message("${dep} version: >= ${vernum}-${verdate}-${${dep}_VERHASH} & < ${vernum_max}")
endforeach()
